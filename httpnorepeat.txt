token<prio=1> hosttoken = "[Hh][Oo][Ss][Tt]" ;
token crlf = "\r?\n" ;
token onespace = " " ;
token httpname = "HTTP" ;
token slash = "/" ;
token digit = "[0-9]+" ;
token colon = ":" ;
token optspace = "[ \t]*" ;
token httptoken = "[-!#$%&'*+.^_`|~0-9A-Za-z]+" ;
token httpfield = "[\t\x20-\x7E\x80-\xFF]*" ;
token period = "[.]" ;
token uri = "[]:/?#@!$&'()*+,;=0-9A-Za-z._~%[-]+" ;
token foldstart = "\r?\n[ \t]+" ;

hostFoldField = httpfield<cb=print> hostFoldFieldEnd ;
hostFoldFieldEnd =
| foldstart action<cb=printsp> httpfield<cb=print> hostFoldFieldEnd ;

httpFoldField = httpfield httpFoldFieldEnd ;
httpFoldFieldEnd =
| foldstart httpfield httpFoldFieldEnd ;


headerField =
  hosttoken colon optspace hostFoldField
| httptoken colon optspace httpFoldField
  ;
version = httpname slash digit period digit ;
requestLine = httptoken onespace uri onespace version crlf ;
requestHdrs = | headerField crlf requestHdrs;
requestWithHeaders = requestLine requestHdrs crlf ;

directive main = requestWithHeaders;
directive parsername = http;
