%{
#include "httprespcommon.h"
%}

token<prio=1,i> cltk = "Content-Length" ;
token crlf = "\r?\n" ;
token onesp = " " ;
token vers = 
  "HTTP/[0-9]+[.][0-9]+" ;
token cod =
  "[0-9][0-9][0-9]" ;
token colonsp = ":[ \t]*" ;
token httptk =
  "[-!#$%&'*+.^_`|~0-9A-Za-z]+" ;
token httpfield =
  "[\t\x20-\x7E\x80-\xFF]*" ;
token foldstart = "\r?\n[\t ]+" ;

httpFieldPart = httpfield httpFieldPartEnd ;
httpFieldPartEnd =
| foldstart httpfield httpFieldPartEnd ;

clFieldPart =
  httpfield<cb=sztxt> clFieldPartEnd ;
clFieldPartEnd =
| foldstart action<cb=invalidate>
  httpfield clFieldPartEnd ;

headerField =
  cltk colonsp clFieldPart
| httptk colonsp httpFieldPart
  ;

respLine =
  vers onesp cod onesp httpfield crlf;

respHdrs = | headerField crlf respHdrs ;
respWithHdrs = respLine respHdrs crlf bytes ;

directive main = respWithHdrs ;
directive parsername = httpresp ;
